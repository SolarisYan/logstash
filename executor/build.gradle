apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'groovy'

mainClassName = "org.apache.mesos.logstash.executor.LogstashExecutor"



sourceCompatibility = 1.7
version='1.0'



buildscript {
    repositories { jcenter { url "http://jcenter.bintray.com/"} }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.mesos:mesos:0.21.1'
    compile "log4j:log4j:1.2.16"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

shadowJar {
    baseName = "logstash-executor-dependencies"

    exclude 'org/apache/mesos/logstash' // only include dependencies
}

jar {
    baseName = "logstash-executor"
    manifest { attributes 'Main-Class': 'org.apache.mesos.logstash.executor.LogstashExecutor' }
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar', 'jar'

    from        "build/libs/logstash-executor-${project.version}.jar"
    from        "build/libs/logstash-executor-dependencies-${project.version}-all.jar"

    into        'build/docker'

    rename { String fileName ->
        fileName.replace("-${project.version}-all", "").replace("-${project.version}", "")
    }
}


task buildDockerImage(type: Exec) {

    executable "docker"
    args "build"
    args "-t"
    args "docker.io/epeld/logstash-executor"
    args "."
}

task pushDockerImage(type: Exec) {
    dependsOn buildDockerImage

    executable "docker"
    args "push"
    args "-f"
    args "docker.io/epeld/logstash-executor"
}


build.dependsOn pushDockerImage
