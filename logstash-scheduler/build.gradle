apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'spring-boot'
apply plugin: 'de.undercouch.download'

mainClassName = "org.apache.mesos.logstash.scheduler.Application"
ext {
    imageName = imagePrefix + '/logstash-scheduler'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'de.undercouch:gradle-download-task:2.1.0'
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    all*.exclude group:"org.springframework.boot", module: "spring-boot-starter-logging"
}

dependencies {
    compile project(':logstash-commons')
    compile project(':logstash-executor')

    compile "log4j:log4j:$log4jVer" // TODO (pnw) I saw logback used somewhere else. Check

    compile("org.springframework.boot:spring-boot-starter-jetty:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-websocket:${springBootVersion}")
    compile("org.springframework:spring-messaging")

    compile "org.webjars:react:$reactVer"
    compile "org.webjars:react-router:$reactRouterVer"
    compile "org.webjars.bower:stomp-websocket:$stompWebsocketVer"
    compile "org.webjars:sockjs-client:$sockjsClientVer"
    compile "org.webjars:highcharts:$highchartsVer"

    testCompile("org.springframework:spring-test:4.1.7.RELEASE")
}

task copyExecutor(type: Copy) {
    from { project(":logstash-executor").getTasksByName("copyJar", false)[0].outputs.files[0] } // Include executor
    into "$buildDir/resources/main/static"
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

jar {
    dependsOn 'getLogstashZip', 'copyExecutor'
    baseName = "logstash-mesos-scheduler"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // Include dependencies

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Implementation-Version': project.version
        )
    }
}

import de.undercouch.gradle.tasks.download.Download
task getLogstashZip(type: Download) {
    src "https://download.elastic.co/logstash/logstash/logstash-" + "$logstashVer" + ".zip"
    dest new File(buildDir, './resources/main/public/logstash.zip')
    onlyIfNewer true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

task taskCopyFilesForDocker(type: Copy) {
    dependsOn "copyJar"
}

task copyJar(type: Copy) {
    dependsOn   'copyExecutor'
    from        "build/libs/logstash-mesos-scheduler-${project.version}.jar"
    from        "start-scheduler.sh"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}
