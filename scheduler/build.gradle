apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'groovy'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

mainClassName = "org.apache.mesos.logstash.scheduler.Application"

buildscript {
    repositories {
        jcenter{ url "http://jcenter.bintray.com/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

run {
    args "-m", "192.168.1.106:5050", "-f", "./conf.conf"
    jvmArgs "-Djava.library.path=/usr/local/lib"
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.mesos:mesos:0.22.1'
    compile "commons-cli:commons-cli:1.0"
    compile "log4j:log4j:1.2.16"

    compile project(':common')

    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

shadowJar {
    baseName = "logstash-scheduler-dependencies"

    exclude 'org/apache/mesos/logstash' // only include dependencies
}

jar {
    dependsOn 'createConfig'
    baseName = "logstash-scheduler"
    manifest {
        attributes (
            'Main-Class': 'org.apache.mesos.logstash.scheduler.Application',
            'Class-Path':'logstash-scheduler-dependencies.jar'
        )
    }
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar', 'jar'

    into        'build/docker'


    from        "build/libs/logstash-scheduler-${project.version}.jar"
    from        "build/libs/logstash-scheduler-dependencies-${project.version}-all.jar"

    rename { String fileName ->
        fileName.replace("-${project.version}-all", "").replace("-${project.version}", "")

    }
}

task buildDockerImage(type: Exec) {
    dependsOn 'copyJar'

    executable "docker"
    args "build"
    args "-t"
    args schedulerImageName
    args "."
}

import org.apache.tools.ant.filters.ReplaceTokens

task createConfig(type: Copy) {
    from 'executor-name.txt'
    into 'build/resources/main/'
    filter(ReplaceTokens, tokens: [executorImageName: executorImageName])
}

build.dependsOn createConfig
build.dependsOn buildDockerImage
build.dependsOn copyJar