apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'spring-boot'
apply plugin: 'application'


def springBootVer = "1.2.5.RELEASE"

buildscript {
    repositories {
        jcenter { url "http://jcenter.bintray.com/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE"
    }
}

applicationDefaultJvmArgs = ["-Dlogstash-mesos.masterUrl=localhost"]


mainClassName = "org.apache.mesos.logstash.scheduler.Application"

run {
    jvmArgs "-Djava.library.path=/usr/local/lib"
}

configurations {
    // We are using jetty instead of tomcat.
    // See, http://docs.spring.io/spring-boot/docs/current/reference/html/howto-embedded-servlet-containers.html#howto-use-jetty-instead-of-tomcat
    compile.exclude module: "spring-boot-starter-tomcat"
}


dependencies {
    compile project(':common')

    compile 'commons-lang:commons-lang:2.6'

    compile 'org.slf4j:slf4j-api:1.7.12'
    runtime 'ch.qos.logback:logback-classic:1.1.3'

    compile("org.springframework.boot:spring-boot-starter-jetty:${springBootVer}")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVer}")
    compile("org.springframework.boot:spring-boot-starter-websocket:${springBootVer}")
    compile("org.springframework:spring-messaging")

    compile 'org.webjars:react:0.13.3'
    compile 'org.webjars:react-router:0.13.2'
    compile 'org.webjars.bower:stomp-websocket:2.3.4'
    compile 'org.webjars:sockjs-client:1.0.0'
    compile 'org.webjars:highcharts:4.1.6'

    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
    testCompile 'junit:junit:4.11'
}

shadowJar {
    baseName = "logstash-scheduler-dependencies"
    exclude 'org/apache/mesos/logstash' // only include dependencies
}

mainClassName ='org.apache.mesos.logstash.scheduler.Application';


jar {
    baseName = "logstash-scheduler"
    manifest {
        attributes(
                'Main-Class': 'org.apache.mesos.logstash.scheduler.Application',
                'Class-Path': 'logstash-scheduler-dependencies.jar'
        )
    }
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar', 'jar'

    into 'build/docker'


    from "build/libs/logstash-scheduler-${project.version}.jar"
    from "build/libs/logstash-scheduler-dependencies-${project.version}-all.jar"

    rename { String fileName ->
        fileName.replace("-${project.version}-all", "").replace("-${project.version}", "")
    }
}

task buildDockerImage(type: Exec) {
    dependsOn 'copyJar'

    executable "docker"
    args "build"
    args "-t"
    args "mesos/logstash-scheduler"
    args "."
}

build.dependsOn buildDockerImage
